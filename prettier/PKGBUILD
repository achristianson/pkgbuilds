# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>

pkgname=prettier
pkgver=1.14.0
pkgrel=1
pkgdesc="An opinionated code formatter for JavScript, ES2017, JSX, Flow, TypeScript, JSON, CSS, LESS, SCSS, GraphQL, Markdown, YAML"
arch=("any")
url="https://prettier.io/"
license=("MIT")
depends=("nodejs")
makedepends=("yarn")
source=("https://github.com/$pkgname/$pkgname/archive/$pkgver.tar.gz")
sha256sums=('ac3f10aa6fefc312674cea2d71ba67621adeed458d8902b8c1a18256c43b252c')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    yarn
    yarn build
}

package() {
    install -d "$pkgdir/usr/lib/$pkgname"
    cd "$pkgdir/usr/lib/$pkgname"
    cp -a "$srcdir/$pkgname-$pkgver/dist/"* .

    install -d "$pkgdir/usr/bin"
    ln -s "/usr/lib/$pkgname/bin-$pkgname.js" "$pkgdir/usr/bin/$pkgname"
}

# vim:set ts=4 sw=4 et:
