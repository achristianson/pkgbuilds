# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>

pkgname=bash-language-server
pkgver=1.4.0
pkgrel=3
pkgdesc="Bash language server implementation based on Tree Sitter and its grammar for Bash"
arch=("x86_64")
url="https://github.com/mads-hartmann/bash-language-server"
license=("MIT")
depends=("nodejs" "acorn")
makedepends=("yarn" "typescript" "python2")
source=("https://github.com/mads-hartmann/$pkgname/archive/server-$pkgver.tar.gz")
sha256sums=('f47af22f66fcd2f4c271a05445851e3c492215adc3198dad18693c820f47ea96')

build() {
    cd "$srcdir/$pkgname-server-$pkgver"
    yarn
    cd server
    yarn run compile
}

package() {
    install -d "$pkgdir/usr/lib/$pkgname"
    cd "$pkgdir/usr/lib/$pkgname"
    cp -a "$srcdir/$pkgname-server-$pkgver/server/"* .

    rm -r node_modules/acorn

    install -d "$pkgdir/usr/bin"
    ln -s "/usr/lib/$pkgname/bin/main.js" "$pkgdir/usr/bin/$pkgname"
}

# vim:set ts=4 sw=4 et:
