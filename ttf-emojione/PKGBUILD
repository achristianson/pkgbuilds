# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Maintainer: Mohammadreza Abdollahzadeh <morealaz at gmail dot com>

pkgname=(ttf-emojione ttf-emojione-alt)
_pkgname=ttf-emojione
pkgver=4.0
pkgrel=2
pkgdesc="Official colorful EmojiOne font"
url="https://github.com/emojione/emojione-assets"
arch=(any)
license=(custom)
depends=('fontconfig')
provides=('ttf-emoji')
replaces=('emojione-fonts')
install=ttf-emojione.install
source=("emojione-android-${pkgver}.ttf"
        "emojione-android-alt-${pkgver}.ttf"
        "70-emojione.conf"
        "license-free.pdf")
# source=("emojione-android-${pkgver}.ttf::https://github.com/emojione/emojione-assets/releases/download/${pkgver}/emojione-android.ttf"
#         "emojione-android-alt-${pkgver}.ttf::https://github.com/emojione/emojione-assets/releases/download/${pkgver}/emojione-android-alt.ttf"
#         "70-emojione.conf"
#         "license-free.pdf")
sha256sums=('8955dd64a7d1db5d476392c4bfc70e155c5d2caf74a48539b08a818830ebbafb'
            '77f08d736d08d8ff90809df1bf6d1a143d46e1214fcf7de6275a520f7a0057ee'
            '71b8b2e9e7d01850e2a753c60e7da1235de20e958e8011bff69a0ea766412c3d'
            '2898feeb41e03acd1a655ba5ddad0072aac83a014ee184fdcdebc7a66d353650')

package_ttf-emojione() {
  conflicts=(ttf-emojione-alt)

  package_common
  install -Dm644 emojione-android-${pkgver}.ttf "${pkgdir}/usr/share/fonts/emojione/emojione.ttf"
}

package_ttf-emojione-alt() {
  conflicts=(ttf-emojione)

  package_common
  install -Dm644 emojione-android-alt-${pkgver}.ttf "${pkgdir}/usr/share/fonts/emojione/emojione.ttf"
}

package_common() {
  install -d "${pkgdir}/etc/fonts/conf.avail/"
  install -t "${pkgdir}/etc/fonts/conf.avail/" -m644 70-emojione.conf
  install -Dm644 license-free.pdf "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE.pdf"
}

# vim:set ts=2 sw=2 et:
