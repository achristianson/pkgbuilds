# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: emersion <contact emersion.fr>

_name=browserpass-native
pkgname=browserpass
pkgver=3.0.2
pkgrel=1
pkgdesc="Browser extension for zx2c4's pass (password manager)"
arch=('x86_64')
url="https://github.com/${pkgname}/${_name}"
license=('ISC')
depends=('gnupg')
makedepends=('go-pie' 'git')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        "${pkgname}-${pkgver}.tar.gz.sig::${url}/releases/download/${pkgver}/${pkgver}.tar.gz.sig")
sha256sums=('57a7c21fd6d8e1413664ec9ffd7fb43282197ede37a2608f92cb9c95966af366'
            'SKIP')
validpgpkeys=('EB4F9E5A60D32232BB52150C12C87A28FEAC6B20')

prepare() {
    cd "${_name}-${pkgver}"
    make configure
}

build() {
    cd "${_name}-${pkgver}"
    make "${pkgname}"
}

check() {
    cd "${_name}-${pkgver}"
    make test
}

package() {
    cd "${_name}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}

# vim:set ts=4 sw=4 et:
